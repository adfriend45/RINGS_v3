!======================================================================!
SUBROUTINE GROW
!----------------------------------------------------------------------!
USE DOUBLE
USE CONTROL
USE PARAMS
USE STATE
USE VARIABLES
!----------------------------------------------------------------------!
IMPLICIT NONE
!----------------------------------------------------------------------!
! Radial length growth (µm day-1).
!----------------------------------------------------------------------!
REAL(DP), DIMENSION (nfi, ncells_max) :: dL
!----------------------------------------------------------------------!
! Potential primary wall-mass growth with unlimiting
! carbohydrates (mg day-1).
!----------------------------------------------------------------------!
REAL(DP) :: dMp
!----------------------------------------------------------------------!
! Cell-cell carbohydrate flux to test SUC solution (mg day-1).
!----------------------------------------------------------------------!
REAL(DP) :: flux
!----------------------------------------------------------------------!
! Cumulative carbohydrate flux to test SUC solution (mg day-1).
!----------------------------------------------------------------------!
REAL(DP) :: rflux
!----------------------------------------------------------------------!
! Temperature-limited rate of cell radial length
! growth (µm-1 µm-1 day-1).
!----------------------------------------------------------------------!
REAL(DP) :: r
REAL(DP) :: Vc ! Cell volume (ml cell-1)
REAL(DP) :: Vl ! Lumen volume (ml cell-1)
REAL(DP) :: Vw ! Wall volume (ml cell-1)
!----------------------------------------------------------------------!
! Set all length and C-unlimited mass increments to 0.
!----------------------------------------------------------------------!
dL     (:,:) = 0.0_DP
dM_max (:,:) = 0.0_DP
!----------------------------------------------------------------------!
! For Figure 2.
!----------------------------------------------------------------------!
!IF (kyr == 1995) T = T + 2.0_DP
!----------------------------------------------------------------------!
! Radial length growth requires non-dormancy.
!----------------------------------------------------------------------!
IF (.NOT. (dorm)) THEN
 !---------------------------------------------------------------------!
 ! Temperature-limited rate of cell radial length
 ! growth (µm-1 µm-1 day-1).
 !---------------------------------------------------------------------!
 !T = T - 2.0_DP
 r = r0 * EXP (-Ea / (kb * T)) / EXP (-Ea / (kb * (tf + 10.0_DP)))
 !T = T + 2.0_DP
 !---------------------------------------------------------------------!
 ! Loop over cells to compute radial and C-unlimited primary wall
 ! growth increments.
 !---------------------------------------------------------------------!
 DO fi = 1, nfi
  DO ic = 1, ncells (fi)
   !-------------------------------------------------------------------!
   ! For Figure 3.
   !-------------------------------------------------------------------!
   !IF ((kyr == 1995) .AND. (D (fi,ic) <= pz)) THEN
   !IF ((kyr == 1995) .AND. (D (fi,ic) > pz)) THEN
   ! T = T + 2.0_DP
   ! r = r0 * EXP (-Ea / (kb * T)) / EXP (-Ea / (kb * (tf + 10.0_DP)))
   ! T = T - 2.0_DP
   !ELSE
   ! r = r0 * EXP (-Ea / (kb * T)) / EXP (-Ea / (kb * (tf + 10.0_DP)))
   !END IF
   !-------------------------------------------------------------------!
   ! Check if cell is in enlargement or proliferation zone.
   !-------------------------------------------------------------------!
   IF (D (fi,ic) <= ez) THEN
    !------------------------------------------------------------------!
    ! Cell radial length increment (µm µm-1 day-1).
    !------------------------------------------------------------------!
    dL (fi,ic) = L (fi,ic) * (EXP (rasym (fi,ic) * r) - 1.0_DP)
    !------------------------------------------------------------------!
    ! Total cell volume (ml cell-1).
    !------------------------------------------------------------------!
    Vc = tal * ttl * L (fi,ic) / 1.0D12
    !------------------------------------------------------------------!
    ! Lumen volume (ml cell-1).
    !------------------------------------------------------------------!
    Vl = (tal - 2.0_DP * t_pw) * (ttl - 2.0_DP * t_pw) * &
         (L (fi,ic) - 2.0_DP * t_pw) / 1.D12
    !------------------------------------------------------------------!
    ! Potential rate of primary cell-wall growth (mg cell-1 day-1).
    !------------------------------------------------------------------!
    dMp = Vl * m0 * EXP (-Eaw / (kb * T)) / &
          EXP (-Eaw / (kb * (tf + 10.0_DP)))
    !------------------------------------------------------------------!
    ! The maximum possible wall volume based on primary wall
    ! thickness (mg cell-1).
    !------------------------------------------------------------------!
    Vw = Vc - Vl
    !------------------------------------------------------------------!
    ! Restrict wall mass growth with unlimiting carbohydrates to value
    ! obtained at the primary wall thickness (mg cell-1 day-1).
    !------------------------------------------------------------------!
    dM_max (fi,ic) = MIN (dMp, rho_w * Vw - M (fi,ic))
    dM_max (fi,ic) = MAX (0.0_DP, dM_max (fi,ic))
   !-------------------------------------------------------------------!
   END IF
  END DO ! ic
 END DO ! fi
END IF ! .NOT. (dorm)
!----------------------------------------------------------------------!
! Loop over cells to compute potential secondary wall growth.
!----------------------------------------------------------------------!
DO fi = 1, nfi
 DO ic = 1, ncells (fi)
  !--------------------------------------------------------------------!
  ! Check cell is in wall thickening zone.
  !--------------------------------------------------------------------!
  IF ((D (fi,ic) > ez) .AND. (D (fi,ic) <= tz)) THEN
   !-------------------------------------------------------------------!
   ! Total cell volume (ml cell-1).
   !------------------------------------------------------------------!
   Vc = tal * ttl * L (fi,ic) / 1.0D12
   !------------------------------------------------------------------!
   ! Wall volume (ml cell-1).
   !------------------------------------------------------------------!
   Vw = M (fi,ic) / rho_w
   !------------------------------------------------------------------!
   ! Lumen volume (ml).
   !------------------------------------------------------------------!
   Vl = Vc - Vw
   !------------------------------------------------------------------!
   ! Wall mass growth with unlimiting carbohydrates (mg cell-1 day-1).
   !------------------------------------------------------------------!
   dM_max (fi,ic) = Vl * m0 * EXP (-Eaw / (kb * T)) / &
                    EXP (-Eaw / (kb * (tf + 10.0_DP)))
  !--------------------------------------------------------------------!
  END IF ! Wall-thickening zone.
  !--------------------------------------------------------------------!
 END DO ! ic
END DO ! fi
!----------------------------------------------------------------------!
! Compute equilibrium carbohydrate concentrations in each cell.
!----------------------------------------------------------------------!
SUC (:,:) = 0.0_DP
CALL CARB
!----------------------------------------------------------------------!
! Following for fort.97_sS. Also comment-out test at end.
!----------------------------------------------------------------------!
!dM = dM_max
!----------------------------------------------------------------------!
! Increment cell radial lengths and wall masses.
!----------------------------------------------------------------------!
DO fi = 1, nfi
 DO ic = 1, ncells (fi)
  L (fi,ic) = L (fi,ic) + dL (fi,ic)
  M (fi,ic) = M (fi,ic) + dM (fi,ic)
 END DO
END DO
!----------------------------------------------------------------------!
! Test carbohydrate distribution solution.
!----------------------------------------------------------------------!
IF (kday == 250) THEN
!IF (kday == 2500) THEN ! For saturating carbohydrates.
 DO fi = 1, nfi
  rflux = 0.0_DP
  DO ic = ncells (fi), 2, -1
   IF (dM(fi,ic) > 0.0_DP) THEN
    flux = (SUC (fi, ic-1) - SUC (fi,ic)) / res - rflux
    rflux = rflux + flux
    IF (ABS (dM(fi,ic) - flux) > 1.D-5) THEN
     WRITE (*,*) 'Problem with flux'
     WRITE (*,*) fi,ic,SUC(fi,ic),dM(fi,ic),flux,D(fi,ic),tz
     STOP
    END IF
   END IF
  END DO
 END DO
END IF
!----------------------------------------------------------------------!
END SUBROUTINE GROW
!======================================================================!
